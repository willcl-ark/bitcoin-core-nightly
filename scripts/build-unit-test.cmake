# Default options (can be overridden via -D)
if(NOT DEFINED WITH_UPDATE)
  set(WITH_UPDATE FALSE)
endif()
if(NOT DEFINED MODEL)
  set(MODEL "Experimental")
endif()
if(NOT DEFINED GENERATOR)
  set(CTEST_CMAKE_GENERATOR "Ninja")
else()
  set(CTEST_CMAKE_GENERATOR ${GENERATOR})
endif()
if(NOT DEFINED WITH_COVERAGE)
  set(WITH_COVERAGE FALSE)
endif()
if(NOT DEFINED WITH_MEMCHECK)
  set(WITH_MEMCHECK FALSE)
endif()
if(NOT DEFINED MEMCHECK_TYPE)
  set(MEMCHECK_TYPE "None")
endif()
if(NOT DEFINED WITH_CLANG_TIDY)
  set(WITH_CLANG_TIDY FALSE)
endif()
if(NOT DEFINED WITH_CPPCHECK)
  set(WITH_CPPCHECK FALSE)
endif()
if(NOT DEFINED WITH_CPPLINT)
  set(WITH_CPPLINT FALSE)
endif()
if(NOT DEFINED WITH_IWYU)
  set(WITH_IWYU FALSE)
endif()
if(NOT DEFINED TREAT_WARNINGS_AS_ERRORS)
  set(TREAT_WARNINGS_AS_ERRORS FALSE)
endif()

# basic build setup
cmake_host_system_information(RESULT HOST_NAME QUERY HOSTNAME)
set(CTEST_SITE ${HOST_NAME})

cmake_host_system_information(RESULT nproc QUERY NUMBER_OF_LOGICAL_CORES)
if(NOT nproc)
  message(WARNING "Could not determine number of logical cores; defaulting to 1.")
  set(nproc 1)
endif()

set(CTEST_BUILD_NAME "${CMAKE_HOST_SYSTEM_PROCESSOR}_${CMAKE_SYSTEM_NAME}")
if(DEFINED BUILD_NAME_SUFFIX)
  set(CTEST_BUILD_NAME "${CTEST_BUILD_NAME}-${BUILD_NAME_SUFFIX}")
endif()

if(NOT CTEST_SOURCE_DIRECTORY)
  set(CTEST_SOURCE_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}")
endif()
if(NOT CTEST_BINARY_DIRECTORY)
  set(CTEST_BINARY_DIRECTORY "${CTEST_SOURCE_DIRECTORY}/build")
endif()

set(CTEST_NOTES_FILES "${CTEST_SOURCE_DIRECTORY}/CMakeLists.txt")

if(MODEL STREQUAL "Nightly")
  set(CTEST_NIGHTLY_START_TIME "7:00:00 UTC")
endif()

# Find required programs
find_program(CTEST_GIT_COMMAND "git")
if(WITH_UPDATE AND NOT CTEST_GIT_COMMAND)
  message(WARNING "Git not found; skipping update.")
  set(WITH_UPDATE FALSE)
endif()

find_program(CTEST_COVERAGE_COMMAND "llvm-cov")
if(WITH_COVERAGE AND NOT CTEST_COVERAGE_COMMAND)
  message(WARNING "llvm-cov not found; disabling coverage.")
  set(WITH_COVERAGE FALSE)
endif()

find_program(CLANG_TIDY "clang-tidy")
if(WITH_CLANG_TIDY AND NOT CLANG_TIDY)
  message(WARNING "clang-tidy not found; disabling clang-tidy.")
  set(WITH_CLANG_TIDY FALSE)
endif()

find_program(CPPCHECK "cppcheck")
if(WITH_CPPCHECK AND NOT CPPCHECK)
  message(WARNING "cppcheck not found; disabling cppcheck.")
  set(WITH_CPPCHECK FALSE)
endif()

find_program(CPPLINT "cpplint")
if(WITH_CPPLINT AND NOT CPPLINT)
  message(WARNING "cpplint not found; disabling cpplint.")
  set(WITH_CPPLINT FALSE)
endif()

find_program(IWYU "include-what-you-use")
if(WITH_IWYU AND NOT IWYU)
  message(WARNING "iwyu not found; disabling iwyu.")
  set(WITH_IWYU FALSE)
endif()

# Memcheck setup
set(CTEST_MEMORYCHECK_COMMAND)
if(WITH_MEMCHECK)
  if(MEMCHECK_TYPE STREQUAL "Valgrind")
    find_program(CTEST_MEMORYCHECK_COMMAND "valgrind")
    if(NOT CTEST_MEMORYCHECK_COMMAND)
      message(WARNING "Valgrind not found; disabling memcheck.")
      set(WITH_MEMCHECK FALSE)
    endif()
    set(CTEST_MEMORYCHECK_TYPE "Valgrind")
    set(ENV{CXXFLAGS} "-g")
    set(ENV{CFLAGS} "-g")
  elseif(MEMCHECK_TYPE STREQUAL "TSAN")
    set(ENV{CXXFLAGS} "-fsanitize=thread -fno-omit-frame-pointer")
    set(ENV{CFLAGS} "-fsanitize=thread -fno-omit-frame-pointer")
    set(CTEST_MEMORYCHECK_TYPE "ThreadSanitizer")
    set(CTEST_MEMORYCHECK_COMMAND "true")
  elseif(MEMCHECK_TYPE STREQUAL "MSAN")
    set(ENV{CXXFLAGS} "-fsanitize=memory -fno-omit-frame-pointer")
    set(ENV{CFLAGS} "-fsanitize=memory -fno-omit-frame-pointer")
    set(CTEST_MEMORYCHECK_TYPE "MemorySanitizer")
    set(CTEST_MEMORYCHECK_COMMAND "true")
  elseif(MEMCHECK_TYPE STREQUAL "ASAN")
    set(ENV{CXXFLAGS} "-fsanitize=address -fno-omit-frame-pointer")
    set(ENV{CFLAGS} "-fsanitize=address -fno-omit-frame-pointer")
    set(CTEST_MEMORYCHECK_TYPE "AddressSanitizer")
    set(CTEST_MEMORYCHECK_COMMAND "true")
  elseif(MEMCHECK_TYPE STREQUAL "UBSAN")
    set(ENV{CXXFLAGS} "-fsanitize=undefined -fno-omit-frame-pointer")
    set(ENV{CFLAGS} "-fsanitize=undefined -fno-omit-frame-pointer")
    set(CTEST_MEMORYCHECK_TYPE "UndefinedBehaviorSanitizer")
    set(CTEST_MEMORYCHECK_COMMAND "true")
  else()
    message(WARNING "Invalid MEMCHECK_TYPE '${MEMCHECK_TYPE}'; disabling memcheck. Valid: Valgrind, TSAN, MSAN, ASAN.")
    set(WITH_MEMCHECK FALSE)
  endif()
endif()

# Coverage setup
if(WITH_COVERAGE)
  set(CTEST_COVERAGE_EXTRA_FLAGS "gcov")
  set( ENV{CFLAGS} "--coverage")
	set(ENV{CXXFLAGS} "--coverage")
	set(ENV{LDFLAGS} "--coverage")
endif()

# Collect configure options
set(CONFIG_OPTIONS)
if(DEFINED CMAKE_ARGS)
  list(APPEND CONFIG_OPTIONS ${CMAKE_ARGS})
endif()
if(WITH_CLANG_TIDY)
  list(APPEND CONFIG_OPTIONS "-DCMAKE_CXX_CLANG_TIDY=${CLANG_TIDY}")
endif()
if(WITH_CPPCHECK)
  list(APPEND CONFIG_OPTIONS "-DCMAKE_CXX_CPPCHECK=${CPPCHECK}")
endif()
if(WITH_CPPLINT)
  list(APPEND CONFIG_OPTIONS "-DCMAKE_CXX_CPPLINT=${CPPLINT}")
endif()
if(WITH_IWYU)
  list(APPEND CONFIG_OPTIONS "-DCMAKE_CXX_INCLUDE_WHAT_YOU_USE=${IWYU}")
endif()

# Start CTest process
ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})

ctest_start(${MODEL})
ctest_submit(PARTS "Notes")

if(WITH_UPDATE AND CTEST_GIT_COMMAND)
  ctest_update()
  ctest_submit(PARTS "Update")
endif()

list(JOIN CONFIG_OPTIONS ";" CONFIG_OPTIONS_STR)
ctest_configure(OPTIONS "${CONFIG_OPTIONS_STR}")
ctest_submit(PARTS "Configure")

ctest_build(
  PARALLEL_LEVEL ${nproc}
  NUMBER_ERRORS build_errors
  NUMBER_WARNINGS build_warnings
)
ctest_submit(PARTS "Build")

message(STATUS "Build errors: ${build_errors}, warnings: ${build_warnings}")
if((build_errors GREATER 0) OR (TREAT_WARNINGS_AS_ERRORS AND (build_warnings GREATER 0)))
  message(FATAL_ERROR "Build cancelled")
endif()

if(WITH_MEMCHECK AND CTEST_MEMORYCHECK_COMMAND)
  ctest_memcheck(PARALLEL_LEVEL ${nproc})
endif()
ctest_submit(PARTS "MemCheck")

ctest_test(PARALLEL_LEVEL ${nproc})
ctest_submit(PARTS "Test")

if(WITH_COVERAGE AND CTEST_COVERAGE_COMMAND)
  ctest_coverage()
  ctest_submit(PARTS "Coverage")
endif()

ctest_submit(PARTS "Done")
